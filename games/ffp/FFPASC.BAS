10 ' FINAL FACULTY PLUS ---------------
20 '
30 ' COPYRIGHT & INITIALIZE -----------
40 CLS :LOCATE 4,0:PRINT CHR$ 234;" COSMIC ENTERTAINMENT GAMEWARE ";CHR$ 234:LOCATE 16,2:PRINT "MINDELIGHT":LINE (85,15)-(94,24),B:LINE (96,24)-(154,24):K=0,X=0
50 CLEAR :RANDOMIZE :DIM S(11),N$(1),A$(7),B$(3),H(1),A(1),C$(15),F$(9),T(1),W$(1),G(59),E$(5),P$(7),M$(5),M(29)
60 DEGREE :C$="FFEFFFFFBFFFFFFDFFFFDFFFFFFFF7DFFFF7FFF7FBEFB7EFDFF76FD7FFFBDFF7FFFFF7BFFFFBFFFFFFEFFFFFBFFFFFFD"
70 FOR I=0TO 19:B$=B$+CHR$ 135:NEXT :Z$="0000000000000000",V$="5665566750657605"
80 FOR I=0TO 7:READ A$(I):NEXT :FOR I=0TO 3:READ B$(I):NEXT
90 FOR I=0TO 7:READ C$(I+I):FOR J=LEN C$(I+I)TO 0STEP -2:C$(I+I+1)=C$(I+I+1)+MID$ (C$(I+I),J+1,2):NEXT :NEXT
100 FOR I=0TO 5:READ E$(I):NEXT :FOR I=0TO 9:READ F$(I):NEXT
110 FOR I=0TO 5:READ M$(I),M(I*5),M(I*5+1),M(I*5+2),M(I*5+3),M(I*5+4):NEXT
120 FOR I=0TO 9:FOR K=0TO 2:G(I*3+K)=COS (I*20+K*120-90)*(24-I*2),G(I*3+30+K)=19+SIN (I*20+K*120-90)*(24-I*2):NEXT :NEXT :K=0
130 GCURSOR (86,23):GPRINT MID$ (C$,X+X+1,16):FOR W=0TO 79:IF INKEY$ LET K=1,W=79
140 NEXT :X=-(X+1)*(X<39):IF K=0GOTO 130
150 '
160 ' TITLE ---------------------------
170 CLS :GCURSOR (12,15):GPRINT "BFDF0D":LINE (8,15)-(16,8):LINE (10,16)-(95,16)
180 FOR I=0TO 12:LOCATE 3,1:PRINT RIGHT$ ("FINAL FACULTY",I+1):NEXT
190 GCURSOR (74,24):GPRINT "1F05050502001F101010000F1010100F001215151508"
200 GOSUB 740:GOSUB 710:GOSUB 770:LOCATE 23,2:PRINT "1.VERSUS":LOCATE 23,3:PRINT "2.STORY"
210 GOSUB 710:M=K-49:IF M<>0AND M<>1GOTO 210ELSE ON M+1GOTO 240,280
220 '
230 ' VERSUS MODE ---------------------
240 FOR P=0TO 1:T$="PLAYER"+STR$ (P+1):GOSUB 800:NEXT
250 FOR P=0TO 1:FOR I=0TO 3:P$(P*4+I)=C$(S(5+P*6)*4+I):NEXT :NEXT :E=7,B=RND 4-1:GOSUB 370:GOTO 170
260 '
270 ' STORY MODE ----------------------
280 T$="ROGUE",P=1:GOSUB 800:FOR I=0TO 3:P$(I+4)=C$(S(11)*4+I):NEXT :CLS :LOCATE 10,1:PRINT "SELECT ENEMY ?"
290 LINE (0,31)-(239,31):FOR I=0TO 3:LOCATE 0,I:GCURSOR (0,7+I*8):GPRINT V$;V$;V$:NEXT :LINE (23,0)-(23,31):LINE (23,14)-(27,31),B
300 FOR I=0TO 2:GCURSOR (70+I*48,30):GPRINT E$(2-I):NEXT :E=0,W=0,B=3
310 GCURSOR (180-W*48,30):GPRINT Z$:GCURSOR (180-E*48,30):GPRINT P$(5):W=E
320 GOSUB 710:E=E-(K=15AND E<3)+(K=14AND E>0):IF K<>13GOTO 310ELSE IF E<3GOTO 340
330 IF E=3GOSUB 1110
340 GOSUB 1080:GOSUB 370:IF R=2GOTO 170ELSE E=E+1:IF E<6GOTO 330ELSE 170
350 '
360 ' BATTLE INIT ---------------------
370 CLS :FOR P=0TO 1:H(P)=S(P*6),A(P)=S(4+P*6),T(P)=0:IF A(P)LET W$(P)="ÀÀ¶³     ><"+LEFT$ (A$(A(P)-1)+"          ",10)
380 LOCATE 30*P,0:PRINT N$(P):LOCATE 1+30*P,1:PRINT USING "####/####";H(P);H(P)
390 LOCATE 30*P,2:PRINT "TIME":LINE (24+180*P,17)-(58+180*P,22),B:IF A(P)LOCATE P*30,3:PRINT A$(A(P)-1)
400 NEXT :GCURSOR (60,7):FOR I=0TO 9:GPRINT B$(B);:NEXT
410 GCURSOR (102,23):GPRINT "00FF81F515151DFF81FFC3BDBDADCBFF81F714F781FF05FD81FD05FFA1FFA1FF00"
420 FOR I=0TO 1:FOR P=0TO 1:GCURSOR (83+P*69,23):GPRINT P$(2+P*4-I-I+P):NEXT :FOR W=0TO 99:NEXT :NEXT
430 FOR W=0TO 299:NEXT :LOCATE 17,2:PRINT "      ":P=0,Z=0
440 '
450 ' BATTLE MAIN ---------------------
460 T(P)=T(P)+S(3+P*6)/50:GCURSOR (26+T(P)+P*180,23):GPRINT 90:IF T(P)<30LET P=-(P=0):GOTO 460
470 LOCATE 30-P*30,2:PRINT "WAIT":U=P
480 FOR I=0TO 3:LINE (25+180*P,18)-(57+180*P,21),X,B:NEXT :C=0:IF A(P)<1GOTO 530ELSE IF E=7OR P=1GOTO 510
490 IF A(0)<3AND H(0)<S(0)*0.6LET C=1:GOTO 530
500 IF A(0)>2AND RND 6>1LET C=1:GOTO 530ELSE 530
510 LOCATE P*30,3:PRINT MID$ (W$(P),C*10+1,10):GOSUB 710:IF K=13GOTO 530ELSE M=(K=15AND C=1)-(K=14AND C=0):IF M=0GOTO 510
520 X=C*10:FOR I=0TO 8:X=X+M:LOCATE P*30,3:PRINT MID$ (W$(P),X+1,10):NEXT :C=C+M:GOTO 510
530 GCURSOR (91+P*53,23):GPRINT P$(P*4+P):GCURSOR (83+P*69,23):GPRINT Z$:IF A(P)<1OR C=0GOSUB 980:GOTO 550
540 ON A(P)GOSUB 1180,1230,1300,1350,1420,1520,1580,1630:A(P)=0
550 LOCATE 13+C*11,3:PRINT "    "
560 GCURSOR (83+P*69,23):GPRINT P$(P*4+P):GCURSOR (91+P*53,23):GPRINT Z$:LINE (26+180*P,19)-(56+180*P,20),R,BF:T(P)=0
570 T(1-P)=T(1-P)+Q*S(9-P*6):IF T(1-P)>29LET T(1-P)=29
580 IF A(P)>0LET P$=A$(A(P)-1)ELSE P$=""
590 LOCATE 30*P,3:PRINT LEFT$ (P$+"          ",10)
600 LINE (206-180*P,19)-(206-180*P+T(1-P),20),BF:LOCATE 30-P*30,2:PRINT "TIME":IF R<1GOTO 460
610 '
620 ' JUDGEMENT -----------------------
630 P=R-1:FOR I=0TO 9:X=RND 10,Y=RND 10:LINE (81+P*69+X,13+Y)-(84+P*69+X,16+Y),BF:NEXT
640 FOR I=0TO 1:LINE (81+P*69+I,13)-(95+P*69+I,26),R,&H5555,BF:FOR W=0TO 99:NEXT :NEXT
650 C=1-P,K=C:FOR I=0TO 11:GCURSOR (83+C*69,23):GPRINT P$(C*4+K):K=-(K=0):NEXT :K=P
660 FOR I=0TO 5:GCURSOR (83+C*69,23):GPRINT P$(C*4+K+K+C):K=-(K=0):FOR W=0TO 299:NEXT :NEXT :RETURN
670 '
680 GOSUB 710:' SUB ROUTINE -----------
690 '
700 ' SUB - KEY INPUT -----------------
710 K=0:WHILE K=0:K=ASC INKEY$ :WEND :WHILE INKEY$ :WEND :RETURN
720 '
730 ' SUB - PAINT BLACK ---------------
740 FOR I=0TO 3:LOCATE 20,I:PRINT B$:NEXT :RETURN
750 '
760 ' SUB - PAINT WHITE ---------------
770 FOR I=0TO 2:LOCATE 22,I+1:PRINT "                  ":NEXT :RETURN
780 '
790 ' SUB - CHR MAKING ----------------
800 GOSUB 740:GOSUB 770:LOCATE 23,2:PRINT T$;" NAME ?":LOCATE 23,3:PRINT "__________"
810 N$(P)="",M$="":LOCATE 23,3:INPUT N$(P):N$(P)=LEFT$ (N$(P),10):IF N$(P)GOSUB 740ELSE 810
820 IF LEN M$<10LET M$=M$+N$(P)
830 A=0:FOR I=0TO 9:A=A+ASC MID$ (M$,I+1,1):NEXT
840 FOR I=0TO 3:N=50+I*17,S(I+P*6)=(A/N-INT (A/N))*50:NEXT
850 S(P*6)=2000+S(P*6)*159,S(3+P*6)=20+S(3+P*6)*0.6
860 N=(A/11-INT (A/11))*11,N=-N*(N<8),S(4+P*6)=N
870 S(5+P*6)=(A/4-INT (A/4))*4:RETURN
880 '
890 ' SUB - DAMAGE DISPLAY ------------
900 D=INT D,D$=STR$ D,L=LEN D$-2:GCURSOR (79+C*69,31):FOR I=0TO L:GPRINT F$(VAL MID$ (D$,I+2,1));0;:NEXT
910 IF D<Z LET Z=D
920 A=H(C):IF H(C)+D>S(C*6)LET D=S(C*6)-H(C)
930 IF H(C)+D<1LET D=-H(C)
940 FOR I=0TO 4:A=A+D/6:LOCATE 1+C*30,1:PRINT USING "####";A:NEXT :H(C)=H(C)+D
950 LOCATE 1+C*30,1:PRINT USING "####";H(C):R=-(H(C)<1)*(C+1):RETURN
960 '
970 ' SUB - ATTACK --------------------
980 C=1-P,R=0,Q=0.3:IF RND 24<2GOTO 1040
990 IF RND 20<2LET S=2ELSE S=1
1000 D=-S(1+P*6)*2/(S(2+C*6)+12)*900*S-RND 50:GOSUB 900:IF S=1GOTO 1030
1010 GCURSOR (86+P*40,15):GPRINT "7C4444007C146C007C00047C04007C007C4444007C147C007C4040"
1020 FOR W=0TO 399:NEXT :LOCATE 13+7*P,1:PRINT "      "
1030 A=0:FOR I=0TO 15:LINE (160-P*77,10+A)-(152-P*61,18+A),X:A=-(A+1)*(A<3):NEXT :LOCATE 13+C*11,3:GOTO 1050
1040 GCURSOR (79+C*69,31):GPRINT "3E0418043E003E002E2A3A002E2A3A":FOR W=0TO 399:NEXT
1050 RETURN
1060 '
1070 ' SUB - ENEMY STATUS INIT --------
1080 FOR I=0TO 3:P$(I)=E$(E):NEXT :N$(0)=M$(E):FOR I=0TO 4:S(I)=M(E*5+I):NEXT :RETURN
1090 '
1100 ' SUB - WELCOME TO KINGDOM -------
1110 CLS :FOR I=0TO 3:GCURSOR (84,7+I*8):FOR J=0TO 8:GPRINT V$;:NEXT :NEXT
1120 LINE (84,0)-(84,31):LINE (156,0)-(156,31)
1130 FOR I=0TO 4:LINE (118-I,20)-(121+I,31),B:LINE (119-I,21)-(120+I,30),R,B:FOR W=0TO 249:NEXT :NEXT :RETURN
1140 '
1150 ' ABILITY ------------------------
1160 '
1170 ' ¹±Ù ----------------------------
1180 FOR I=0TO 7:X=RND 6-1,Y=RND 6-1:FOR J=0TO 1:LINE (90+P*53+X,23-Y)-(93+P*53+X,26-Y),X,BF
1190 LINE (90+P*53,20-I*2)-(100+P*53,23-I*2),X,BF:NEXT :NEXT
1200 D=S(P*6)/2,C=P,Q=0.3:GOSUB 900:FOR I=0TO 1:FOR J=0TO 5:LINE (94+P*53-J,18-J)-(95+P*53+J,19+J),X,B:NEXT :NEXT :RETURN
1210 '
1220 ' ÌÙ¹± ---------------------------
1230 W=93+P*53,V=16:LINE (W,V)-(W+3,V+3),X,BF
1240 FOR I=0TO 32:X=93+P*53+COS (I*45)*I,Y=16-SIN (I*45)*4-I/3:LINE (X,Y)-(X+3,Y+3),X,BF:LINE (W,V)-(W+3,V+3),X,BF:W=X,V=Y:NEXT
1250 LINE (W,V)-(W+3,V+3),X,BF
1260 D=S(P*6),C=P,Q=0.5:GOSUB 900:LINE (91+P*53,23)-(99+P*53,0),X,&H5555,BF:FOR I=0TO 5:LINE (91+P*53,23)-(99+P*53,0),X,BF:NEXT
1270 LINE (91+P*53,23)-(99+P*53,0),X,&H5555,BF:RETURN
1280 '
1290 ' ¸Þ×ËÞÃÞ ------------------------
1300 C=1-P,X=87+C*69:FOR I=0TO 1:FOR K=0TO 9:LINE (X+G(K*3),G(K*3+30))-(X+G(K*3+1),G(K*3+31)),X
1310 LINE -(X+G(K*3+2),G(K*3+32)),X:LINE -(X+G(K*3),G(K*3+30)),X:NEXT :NEXT
1320 D=-H(C)/2,Q=0.4:GOSUB 900:FOR I=0TO 1:FOR J=0TO 5:LINE (81+C*69+J,13+J)-(92+C*69-J,24-J),X,B:NEXT :NEXT :RETURN
1330 '
1340 ' »ÝÀÞ- --------------------------
1350 C=1-P:LINE (82+C*69,0)-(91+C*69,3),X,BF:R$="":FOR I=0TO 15:R$=R$+HEX$ (RND 16-1):NEXT :FOR I=0TO 3:FOR J=0TO 1
1360 LINE (82+C*69+VAL ("&H"+MID$ (R$,I*4+1,1))*0.625,4)-(79+C*69+VAL ("&H"+MID$ (R$,I*4+2,1)),7),X
1370 FOR K=0TO 2:LINE -(79+C*69+VAL ("&H"+MID$ (R$,I*4+K+3,1)),7+K*8),X:NEXT :NEXT :NEXT
1380 LINE (82+C*69,0)-(91+C*69,3),X,BF:D=-(3500-S(1+C*6)*10):GOSUB 900
1390 FOR I=0TO 1:FOR J=0TO 4:LINE (86+C*69-J,0)-(87+C*69+J,23),X,BF:NEXT :NEXT :RETURN
1400 '
1410 ' ±ÙÃÏ ---------------------------
1420 C=1-P,K=1-P-P:W=93+P*53,V=17:LINE (W,V)-(W+3,V+3),X,BF
1430 FOR I=0TO 9:X=93+P*53+I*3*K,Y=17-I^2/4:LINE (X,Y)-(X+3,Y+3),X,BF:LINE (W,V)-(W+3,V+3),X,BF:W=X,V=Y:NEXT
1440 LINE (W,V)-(W+3,V+3),X,BF:FOR I=0TO 2:X=RND 48*2-1:GCURSOR (98,15+I*8):GPRINT MID$ (C$,X,96-X);LEFT$ (C$,X):NEXT
1450 R$="":FOR I=0TO 5:R$=R$+RIGHT$ ("000"+HEX$ RND 65535,3):NEXT
1460 FOR I=0TO 1:FOR J=0TO 6:FOR K=0TO J:LINE (119-K,19-K)-(120+K,20+K),X,B:NEXT :NEXT :NEXT
1470 D=-6000+S(2+C*6)*6-RND 99,Q=0.7:GOSUB 900
1480 FOR I=0TO 1:FOR J=0TO 5:LINE (119+C,16)-(60+118*C,23),X,VAL ("&H"+MID$ (R$,J*3+1,3)),BF:NEXT :NEXT
1490 FOR I=0TO 2:LOCATE 16,1+I:PRINT "         ":NEXT :RETURN
1500 ' 
1510 ' ÊÞ-»¸ --------------------------
1520 X=95+P*53:GCURSOR (X-11,15):GPRINT "2E2A3A00023E02003E0A36003E0418043E003C0A3C00360836"
1530 FOR I=0TO 1:FOR K=0TO 8:W=COS (90-K*17)*10,V=SIN (90-K*17)*10
1540 LINE (X+W,20+V)-(X-W,20-V),X:LINE (X+W,20-V)-(X-W,20+V),X:NEXT :NEXT
1550 S(1+P*6)=50,Q=0.3,R=0:LOCATE 13+P*9,1:PRINT "       ":RETURN
1560 '
1570 ' ÓÉÏÈ ---------------------------
1580 W=93+P*53,V=18,H=1-P-P:LINE (W,V)-(W+3,V+3),X,BF:FOR I=0TO 15
1590 X=93+P*53+H*I*4,Y=SQU (I-7)/3-2:LINE (X,Y)-(X+3,Y+3),X,BF:LINE (W,V)-(W+3,V+3),X,BF:W=X,V=Y:NEXT
1600 D=Z,C=1-P,Q=0.5:GOSUB 900:LINE (W,V)-(W+3,V+3),X,BF:RETURN
1610 '
1620 ' ±Îß¶ØÌß½ -----------------------
1630 C=1-P:FOR I=0TO 7:LINE (83+C*69,16)-(90+C*69,23),X,BF:NEXT
1640 FOR I=0TO 3:W=86+C*69,V=(RND 16-8)/9:LINE (W,19)-(W+3,22),X,BF
1650 FOR J=0TO 8:X=86+C*69+16*V-SQU (J-4)*V:LINE (X,17-J*2)-(X+3,20-J*2),X,BF:LINE (W,19-J*2)-(W+3,22-J*2),X,BF:W=X:NEXT
1660 LINE (X,1)-(X+3,4),X,BF:NEXT :FOR I=0TO 7:FOR J=0TO 5:LINE (86+C*69-J,2-J)-(87+C*69+J,3+J),X,B:NEXT :NEXT
1670 FOR I=0TO 9:LINE (85+C*69,3)-(87+C*69,23),X,BF:NEXT
1680 FOR I=0TO 16:W=SQU (16-I)/7:FOR J=0TO 1:LINE (81+C*69-W,24)-(92+C*69+W,26),X,BF:NEXT :NEXT
1690 D=-7000+S(2+C*6)*70-RND 99:GOSUB 900
1700 FOR I=0TO 1:FOR J=0TO 15:LINE (86+C*69-J,19-J)-(87+C*69+J,20+J),X,B:NEXT :NEXT :Q=0.8:RETURN
1710 '
1720 ' DATA ---------------------------
1730 DATA ¹±Ù,ÌÙ¹±,¸Þ×ËÞÃÞ,»ÝÀÞ-,±ÙÃÏ,ÊÞ-»¸,ÓÉÏÈ,±Îß¶ØÌß½:' ABILITY NAME
1740 DATA 18380C0402162C7870180414,38A818285818289828381848,7EA57E7F267CBA7D7EA53A7F,FD150D050D01FD0D150D0501:' BACK GRAPHIC
1750 DATA 3C27EFEDF9BF251C,0F11FFFDF9BF110F:' CHR1 FIGHTER
1760 DATA 09BFCDFD55D3EEBA,09BFE3FF55D3EE9E:' CHR2 MAGICIAN
1770 DATA 384FDDF5F5BF2830,1E23FFF5F5BF120E:' CHR3 SEEF
1780 DATA 65353FFFF7933E73,351D3FFFF7931E3B:' CHR4 SINGER
1790 DATA 40C0E0E0F0F06040,38FCFEFFBF951704,30B8C5FF3DF39806,9CF2F676FFEDB701,0EBAFFFD972C281F,780DEFF5B71C1408:' ENEMY
1800 DATA 3E223E,003E00,3A2A2E,2A2A3E,0E083E,2E2A3A,3E2A3A,06023E,3E2A3E,2E2A3E:' SMALL FONT
1810 DATA ½×²Ñ,2284,11,18,27,1,ÍÞ±-,5039,24,14,22,4,ÎÞ³Í²,4217,23,22,30,2,¹ÝÎß³¶,4722,34,30,23,6,¿Ù¼Þ¬-,6189,39,28,41,3,´ÝÍß×-,9999,45,28,40,8:' ENEMY
