10 ' FINAL FACULTY --------------------
20 '
30 PRINT "STARTING FINAL FACULTY"
40 CLEAR :RANDOMIZE :DIM G$(23),S(11),N$(1),A$(5),A(3),T(1),H(1):Z$="0000000000000000"
50 FOR I=0TO 5:READ G$(I):FOR J=0TO LEN G$(I)/2-1:G$(I+6)=G$(I+6)+MID$ (G$(I),LEN G$(I)-J-J-1,2):NEXT :NEXT
60 FOR I=0TO 11:READ G$(12+I):NEXT :FOR I=0TO 5:READ A$(I):NEXT
70 CLS :LINE (84,8)-(160,8):FOR X=0TO 12:LOCATE 14,0:PRINT RIGHT$ ("FINAL FACULTY",X+1):FOR W=0TO 9:NEXT :NEXT
80 LOCATE 11,3:PRINT "(C)1999 MINDELIGHT":GOSUB 640
90 FOR P=0TO 1:CLS :N$(P)=""
100 PRINT "INPUT PLAYER";P+1;"NAME ?":INPUT N$(P):N$(P)=LEFT$ (N$(P),10):IF LEN N$(P)<1GOTO 100
110 PRINT "INPUT PLAYER";P+1;"PASSWORD ?":C$="":INPUT C$:W=VAL ("&H"+C$),M$=""
120 IF LEN M$<10LET M$=M$+N$(P):GOTO 120
130 A(P)=0:FOR I=0TO 9:A(P)=A(P)+ASC MID$ (M$,I+1,1):NEXT :IF W GOTO 160
140 S(4+P*6)=(A(P)/14-INT (A(P)/14))*14:IF S(4+P*6)>5LET S(4+P*6)=6
150 GOTO 170
160 IF (W/A(P)-INT (W/A(P)))*A(P)=0LET S(4+P*6)=W/A(P)-1ELSE PAUSE "PASSWORD IS NOT GENUINE":GOTO 100
170 FOR I=0TO 3:N=50+I*12,S(I+P*6)=INT ((A(P)/N-INT (A(P)/N))*2500/N):NEXT
180 S(3+P*6)=10+S(3+P*6)*0.8
190 S(5+P*6)=-(S(4+P*6)<>6)-(S(P*6)>35),S(P*6)=S(P*6)*150+2000,H(P)=S(P*6),T(P)=0,A(P+2)=-(S(4+P*6)<>6),Q=0
200 NEXT :CLS :FOR P=0TO 1:LOCATE P*30,0:PRINT N$(P):LOCATE P*30+1,1:PRINT USING "####/####";H(P);H(P)
210 LOCATE P*30,2:PRINT "TIME":LINE (24+P*180,17)-(58+P*180,22),B:IF S(4+P*6)<6LOCATE P*30,3:PRINT A$(S(4+P*6))
220 GCURSOR (80+P*72,23):GPRINT G$(S(5+P*6)+P*6):NEXT :GCURSOR (60,7):FOR I=0TO 9:GPRINT G$(12);:NEXT
230 LOCATE 17,2:PAUSE "READY":LOCATE 17,2:PRINT "     "
240 K=ASC INKEY$ ,P=-(K=241)-(K=245)*2-1:IF P<0GOTO 310
250 C=P:GOSUB 650:GCURSOR (119+H*22,23):GPRINT G$(3+P*6+S(5+P*6))
260 D=INT ((S(1+P*6)*2-S(8-P*6))*(T(P)^2/24)+RND 10),D=-D*(D>0),C=1-P:GOSUB 680
270 H(C)=H(C)-D,H(C)=-H(C)*(H(C)>0)
280 B=1,H=1-P-P:FOR I=0TO 6:GCURSOR (116+H*(36-B*H),23):GPRINT 0;G$(S(11-P*6)+6-P*6);0:B=-(B=0):FOR W=0TO 24:NEXT :NEXT
290 GCURSOR (119-H*22,23):GPRINT "0000":GOSUB 710:GOSUB 700:C=P:GOSUB 660:GOSUB 670
300 IF H(1-P)>0GOTO 240ELSE 540
310 FOR P=0TO 1:GCURSOR (26+P*180+T(P),23):GPRINT 90:T(P)=T(P)+S(3+P*6)/51:NEXT
320 F=-(T(0)>31)-(T(1)>31)*2:IF F=0GOTO 240
330 P=-(F=2),C=P:GOSUB 650:IF A(P+2)ON S(4+P*6)+1GOTO 340,380,410,440,480,510ELSE 250
340 GOSUB 650:D=S(P*6)/2:GOSUB 680:H=P+P-1:FOR I=0TO 11
350 X=114+H*28+RND 10:FOR J=0TO 3:LINE (X,23)-(X,0),X:NEXT :NEXT
360 H(P)=H(P)+D:IF H(P)>S(P*6)LET H(P)=S(P*6)
370 GOSUB 710:GOSUB 700:GOSUB 660:GOSUB 720:GOSUB 670:GOTO 240
380 GOSUB 650:D=H(1-P)/2,C=1-P:GOSUB 680
390 H=1-P-P,G=1:FOR I=0TO 29:GCURSOR (116+H*36,23):GPRINT G$((S(11-P*6)+6-P*6)*(1-G)+13*G):G=-(G=0):NEXT
400 H(1-P)=H(1-P)/2:GOSUB 710:GOSUB 700:C=P:GOSUB 660:GOSUB 670:GOSUB 720:GOTO 240
410 GOSUB 650:S(3+P*6)=50
420 H=P+P-1:FOR I=0TO 7:LINE (115+H*28,23)-(124+H*28,12),X,BF:NEXT
430 GOSUB 660:GOSUB 670:GOSUB 720:GOTO 240
440 GOSUB 650:D=5000-S(8-P*6)*60+RND 10,C=1-P:GOSUB 680
450 H=1-P-P:FOR J=0TO 1:FOR I=0TO 3:LINE (119+H*36-I,23)-(119+H*36+I,0),X,BF:NEXT :NEXT
460 H(C)=H(C)-D,H(C)=-H(C)*(H(C)>0)
470 GOSUB 710:GOSUB 700:C=P:GOSUB 660:GOSUB 670:GOSUB 720:GOTO 300
480 GOSUB 650:S(1+P*6)=50,H=P+P-1
490 FOR I=0TO 3:FOR J=0TO 6:LINE (119+H*28-J,19-J)-(119+H*28+J,20+J),X,B:NEXT :NEXT
500 GOSUB 660:GOSUB 670:GOSUB 720:GOTO 240
510 GOSUB 650:D=Q,C=1-P:GOSUB 680:B=P,H=1-P-P:FOR I=0TO 29:GCURSOR (116+H*36,23):GPRINT G$(S(C*6+5)+B*6):B=-(B=0):NEXT
520 H(C)=H(C)-D,H(C)=-H(C)*(H(C)>0)
530 GOSUB 710:GOSUB 700:C=P:GOSUB 660:GOSUB 670:GOSUB 720:GOTO 300
540 H=1-P-P,X=115+H*36,G=S(11-P*6)+6-P*6:FOR I=0TO 9:GCURSOR (X+H*I,23):GPRINT 0;G$(G);0:NEXT
550 LOCATE 12,0:PRINT " PLAYER";P+1;"WIN ! ":GCURSOR (X+H*9+1,23):GPRINT Z$:GOSUB 640:IF S(10-P*6)=6GOTO 70
560 CLS :PRINT N$(P);" GOT ";A$(S(10-P*6));" ABILITY PASSWORD"
570 PRINT HEX$ ((S(10-P*6)+1)*A(P)):GOSUB 640:GOTO 70
580 ' DATA ----------------------------
590 DATA 3BE7EDE9BF2D140C,09BBCDFD55D3AE3A,304FDDF5F5BF2830,281F,330F,3B05
600 DATA 221C0804022C183804180804,183C7E7E7E7E3C18
610 DATA 7C447C,7C,74545C,54547C,1C107C,5C5474,7C5474,0C047C,7C547C,5C547C
620 DATA ¹±Ù,¸Þ×ËÞÃÞ,Í²½Ä,»ÝÀÞ-,ÊÞ-»¸,ÓÉÏÈ
630 ' SUB ROUTINE ---------------------
640 WHILE INKEY$ ="":WEND :RETURN
650 H=C+C-1:GCURSOR (116+H*28,23):GPRINT G$(S(5+C*6)+C*6):GCURSOR (116+H*36,23):GPRINT Z$:RETURN
660 H=C+C-1:GCURSOR (116+H*36,23):GPRINT G$(S(5+C*6)+C*6):GCURSOR (116+H*28,23):GPRINT Z$:RETURN
670 T(C)=0:LINE (26+C*180,19)-(56+C*180,20),R,BF:RETURN
680 H=C+C-1,Q=D,D$=STR$ D:GCURSOR (112+H*36,31):FOR X=0TO LEN D$-2
690 GPRINT G$(14+VAL MID$ (D$,X+2,1));"00";:NEXT :RETURN
700 H=C+C-1:GCURSOR (112+H*36,31):GPRINT Z$;Z$:RETURN
710 LOCATE C*30+1,1:PRINT USING "####";H(C):RETURN
720 LOCATE C*30,3:PRINT "       ":A(C+2)=0:RETURN
