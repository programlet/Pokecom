10 ARUN
20 ' Windows 65 ----------------------
30 '
40 ' TITLE LOGO -----------------------
50 CLS :F=FRE 0:FOR C=0TO F STEP 256:LOCATE 0,0:PRINT USING "MEMORY #####B OK";C:NEXT
60 LOCATE 0,0:PAUSE USING "MEMORY #####B OK";F:CLS :PRINT "STARTING WINDOWS 65 ..."
70 CLEAR :DIM P$(47):M$="3F216294E8":FOR I=0TO 19:S$=S$+"  ":NEXT
80 A$="±À×¼²ÌßÛ¸Þ×ÑÉÂ²¶³¨»Þ-ÄÞ"+CHR$ 228:FOR I=0TO 15:A$=A$+CHR$ 135:NEXT
90 CLS :GCURSOR (95,25):GPRINT "0FFFF03C3CF0FF0FF4F400F8F81008F8F070F888D8FFFF70F88888F87038F8C070C0F83890B8A8E8403C5289898952204F8989898971"
100 OPEN "E:PROGRAM.DAT"FOR INPUT AS #1
110 WHILE EOF (1)=0:INPUT #1,P$(P+P),P$(P+P+1):IF L<LEN P$(P+P)LET L=LEN P$(P+P)
120 P=P+1:LINE (X*48,30)-(X*48+47,31),X,BF:X=-(X+1)*(X<4):WEND :CLOSE
130 '
140 ' START BUTTON --------------------
150 CLS :LOCATE 0,3:PRINT "[½À-Ä]":GCURSOR (36,31):GPRINT M$
160 WHILE INKEY$ <>CHR$ 13:WEND
170 '
180 ' START MENU ----------------------
190 LOCATE 0,0:PRINT "|ÌßÛ¸Þ×Ñ     >|"
200 LOCATE 0,1:PRINT "|¾¯Ã²        >|"
210 LOCATE 0,2:PRINT "|Windows¼­³Ø®³|"
220 LOCATE 6,3:PRINT " ":Y=0,V=0
230 LOCATE 15,V:PRINT " ":GCURSOR (90,7+Y*8):GPRINT M$:V=Y
240 GOSUB 610:Y=Y+(K=4AND Y>0)-(K=5AND Y<2):IF K<>13GOTO 230
250 ON Y+1GOTO 310,450,270
260 '
270 ' Windows¼­³Ø®³ -------------------
280 CLS :LOCATE 14,1:PRINT "Îß¹¯ÄºÝËß­-ÀÉ":LOCATE 9,2:PRINT "ÃÞÝ¹ÞÝ¦·Ù¼Þ­ÝËÞ¶ÞÃÞ·Ï¼À":END
290 '
300 ' ÌßÛ¸Þ×Ñ MENU --------------------
310 Y=0,V=0,C=P-1:IF C>3LET C=3
320 IF C>2LOCATE 14,3:PRINT "|"
330 FOR I=0TO C:LOCATE 15,I:PRINT LEFT$ (S$,L+1):LOCATE 15,I:PRINT P$((I+S)*2):LOCATE 15+L,I:PRINT "|":NEXT
340 LOCATE 16+L,V:PRINT " ":GCURSOR ((16+L)*6,7+Y*8):GPRINT M$:V=Y
350 GOSUB 610:ON -(K=4OR K=5)-(K=15)*2-(K=13)*3GOTO 360,400,410:GOTO 350
360 Y=Y+(K=4)-(K=5):IF Y>-1AND Y<C+1GOTO 340
370 IF Y<0LET Y=0,S=S+(S>0)
380 IF Y>C LET Y=C,S=S-(S+Y+1<P)
390 GOTO 330
400 FOR I=0TO 2:LOCATE 15,I:PRINT LEFT$ (S$,L+2):NEXT :LOCATE 14,3:PRINT LEFT$ (S$,L+3):GOTO 190
410 CLS :LOCATE 0,3:PRINT "[½À-Ä][";P$((Y+S)*2);"]":GCURSOR ((16+L)*6,7+Y*8):GPRINT "6355495563"
420 CHAIN P$((Y+S)*2+1)
430 '
440 ' ¾¯Ã² ----------------------------
450 LOCATE 15,1:PRINT "[½À-Ä]ÒÆ­-|":GCURSOR (156,15):GPRINT M$
460 GOSUB 610:ON -(K=13)-(K=15)*2GOTO 500,470:GOTO 460
470 LOCATE 15,1:PRINT LEFT$ (S$,12):GOTO 190
480 '
490 ' [½À-Ä]ÒÆ­- EDIT -----------------
500 CLS :PRINT A$
510 PRINT "INPUT PROGRAM TITLE ?":INPUT N$
520 PRINT "INPUT PROGRAM FILE ?":INPUT F$
530 CLS :PRINT " PROGRAM ";N$:PRINT "FILENAME ";F$
540 PRINT "Ä¦Û¸¼Ã ÖÛ¼² ÃÞ½¶ ?":PRINT "[ O K ]  [·¬Ý¾Ù]":X=0,W=0
550 LOCATE 7+W*9,3:PRINT " ":GCURSOR (42+X*54,31):GPRINT M$:W=X
560 GOSUB 610:X=X-(K=14AND X=0)+(K=15AND X=1):IF K<>13GOTO 550ELSE IF X>0GOTO 190
570 CLS :OPEN "E:PROGRAM.DAT"FOR APPEND AS #1:PRINT #1,N$:PRINT #1,F$:CLOSE
580 CLS :LOCATE 0,2:PAUSE "Windows ¦ »²·ÄÞ³ ¼Ã²Ï½ ...":GOTO 50
590 '
600 ' SUB ROUTINE ---------------------
610 K=0:WHILE K=0:K=ASC INKEY$ :WEND :WHILE INKEY$ :WEND :RETURN
620 '
630 ' FILE CREATE ---------------------
640 OPEN "E:PROGRAM.DAT"FOR OUTPUT AS #1:PRINT #1,"FINAL FACULTY":PRINT #1,"E:FF.BAS":CLOSE
